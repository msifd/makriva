buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url 'https://repo.spongepowered.org/repository/maven-public' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply from: 'defines.gradle'

repositories {
    maven { url 'https://www.cursemaven.com' }
}

version = project.versions.makriva
group = "msifeed.makriva"
archivesBaseName = "makriva"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
compileJava.options.encoding = compileTestJava.options.encoding = 'UTF-8'

minecraft {
    version = project.versions.minecraft
    mappings = 'stable_39'
    makeObfSourceJar = false

    runDir = "run"

    setClientRunArgs([
            "--tweakClass", "org.spongepowered.asm.launch.MixinTweaker",
            "--mixin", "mixins.makriva.json",
            "--username", "msifeed",
    ])
    setServerRunArgs([
            "--tweakClass", "org.spongepowered.asm.launch.MixinTweaker",
            "--mixin", "mixins.makriva.json",
    ])
    setClientJvmArgs([
            "-Dlog4j.configurationFile=log4j2_fix.xml"
    ])
    setServerJvmArgs([
            "-Dlog4j.configurationFile=log4j2_fix.xml"
    ])
}

mixin {
    add sourceSets.main, "makriva.refmap.json"
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed 'org.spongepowered:mixin:0.8.2'
    compile "curse.maven:more-player-models-221939:2837187"

    testCompile 'org.junit.jupiter:junit-jupiter:5.7.0'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }

    manifest.attributes(
            'TweakOrder': 0,
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.makriva.json',

            'FMLCorePluginContainsFMLMod': 'true',
            'ForceLoadAsMod': 'true',
    )
}
