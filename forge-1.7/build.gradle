buildscript {
    repositories {
        mavenCentral()
        maven { url "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
        }
    }
}

apply plugin: 'forge'
apply plugin: 'idea'

version = makrivaVersion + '-1.7-alpha.5'
group = 'msifeed.makriva'
archivesBaseName = 'makriva'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "forge-1.7/run"

    // Add args:
    // --tweakClass org.spongepowered.asm.launch.MixinTweaker --mixin mixins.makriva.json
    // --username msifeed
    // Add JVM args:
    // -Dfml.coreMods.load=msifeed.makriva.mixins.DepsMixinHook
}

// Setup Mixin library manually
def refMapForYourConfig = 'mixins.makriva.refmap.json'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

configurations {
    embed
    compile.extendsFrom embed
}

repositories {
    maven { url 'https://repo.spongepowered.org/repository/maven-public' }
}

dependencies {
    embed project(':shared')

    embed('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        // Mixin includes a lot of dependencies that are too up-to-date
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }

//    compile files('deps/MorePlayerModels_1.7.10b-deobf.jar')
    compile files('deps/mpm-ariadna-1.19.7-deobf.jar')
}

sourceSets {
    main {
        output.resourcesDir = output.classesDir
        ext.refMap = refMapForYourConfig
    }
}

def refMap = "${tasks.compileJava.temporaryDir}" + File.separator + refMapForYourConfig
def mixinSrg = "${tasks.reobf.temporaryDir}" + File.separator + "mixins.srg"

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
    from refMap

    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'MixinConfigs': 'mixins.makriva.json',
            'FMLCorePluginContainsFMLMod': true,
            'FMLCorePlugin': 'msifeed.makriva.mixins.DepsMixinHook',
            'ForceLoadAsMod': true,
    )
}

reobf {
    addExtraSrgFile mixinSrg
}

afterEvaluate {
    tasks.compileJava {
        options.compilerArgs += [
                // There's a bug in the AnnotationProcessor for 0.7.11 that will generate the annotations pointing to the parent class instead of subclass
                // resulting in the mixin not being applied.  This is fixed in 0.8, however 0.8 needs guava > 21.0, and minecraft ships with 17.0.
                // So as a hacky workaround... ship with 0.7.11, but use the AP from 0.8 for compiling
                "-processorpath", projectDir.absolutePath + File.separator + './deps/mixin-0.8-SNAPSHOT.jar',
                "-processor", "org.spongepowered.tools.obfuscation.MixinObfuscationProcessorInjection,org.spongepowered.tools.obfuscation.MixinObfuscationProcessorTargets",
                "-Xlint:-sunapi", "-XDenableSunApiLintControl", "-XDignore.symbol.file",
                "-AreobfSrgFile=${tasks.reobf.srg}", "-AoutSrgFile=${mixinSrg}", "-AoutRefMapFile=${refMap}"
        ]
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea {
    module {
        inheritOutputDirs = true
    }
}
subprojects {
    apply plugin: 'idea'
}